plugins {
  id 'com.android.application'
  id 'dev.flutter.flutter-gradle-plugin'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
  localPropertiesFile.withReader('UTF-8') { reader ->
    localProperties.load(reader)
  }
}

android {
  namespace 'com.noscall.app'
  compileSdk 35
  ndkVersion "27.0.12077973"

  //disable resource file check
  def version = parsePubspecVersion()
  def versionParts = version.split("\\+")
  defaultConfig {
    applicationId "com.noscall.app"
    minSdk 28
    targetSdk 35
    versionCode versionParts[1] as Integer
    versionName versionParts[0]

  }

  splits {
    abi {
      enable true
      reset()
      include 'armeabi-v7a', 'arm64-v8a'
      universalApk true
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      minifyEnabled false
      shrinkResources false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  lint {
    abortOnError false
    checkReleaseBuilds false
  }
}

flutter {
  source '../..'
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation platform('com.google.firebase:firebase-bom:32.0.0')
}

def parsePubspecVersion() {
  def pubspec = file('../../pubspec.yaml')
  def version = '1.0.0+1' // Default value

  if (pubspec.exists()) {
    def contents = pubspec.readLines()
    def versionLine = contents.find { it.startsWith('version:') }
    if (versionLine != null) {
      version = versionLine.split(' ')[1].trim()
    }
  }

  return version
}
